'==========================================================
' GAME INIT
'==========================================================
GraphicsWindow.KeyDown = HandleKey
GraphicsWindow.Clear()
GraphicsWindow.Title = "D.M."
GraphicsWindow.Height = 800
GraphicsWindow.Width = 800
GraphicsWindow.CanResize = "False"
GraphicsWindow.Show()

game["state"] = "pregame"
game["rooms"] = 20 ' number of rooms to escape
LoadPreGame()

'==========================================================
' GAME LOOP
'==========================================================
While ("True")
EndWhile

Sub HandleKey
  key = GraphicsWindow.LastKey
  If key = "Escape" Then
    Program.End()
  EndIf
  
  If game["state"] = "pregame" Then
    UpdatePreGame()
  EndIf
  If game["state"] = "ingame" Then
    UpdateInGame()
  EndIf
  If game["state"] = "open_chest" Then
    UpdateChest()
  EndIf
  If game["state"] = "fight_monster" Then
    UpdateFight()
  EndIf
  If game["state"] = "postgame" Then
    UpdatePostGame()
  EndIf
EndSub

'==========================================================
' START GAME STATE
'==========================================================
Sub LoadPreGame
  GraphicsWindow.Clear()
  
  GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(50, 50, 50)
  GraphicsWindow.FillRectangle(0, 0, GraphicsWindow.Width, GraphicsWindow.Height)
  GraphicsWindow.BrushColor = "Green"
  GraphicsWindow.FontSize = 16
  GraphicsWindow.FontBold = 0
  GraphicsWindow.DrawText(100, 300, "Злой маг заточил вас в магическую башню")
  GraphicsWindow.DrawText(100, 330, "Вам надо подняться на " + game["rooms"] + " этаж, чтобы выбраться")
  GraphicsWindow.DrawText(100, 360, "На этажах вам будут попадаться сундуки и монстры")
  GraphicsWindow.DrawText(100, 390, "В сундуках может быть броня или оружие. Осторожно, некоторые сундуки ядовиты")
  GraphicsWindow.DrawText(100, 420, "От монстров можно сбежать. Но знайте монстры единственный способ добыть еду")
  GraphicsWindow.DrawText(100, 450, "Каждый этаж отнимает у вас здоровье. Вам надо кушать, чтобы выжить")
  GraphicsWindow.BrushColor = "Yellow"
  GraphicsWindow.DrawText(300, 550, "Нажмите Q для начала игры")
EndSub

Sub UpdatePreGame
  key = GraphicsWindow.LastKey
  If key = "Q" Then
    game["state"] = "ingame"
    LoadInGame()
  EndIf
EndSub

'==========================================================
' GAME STATE
'==========================================================
Sub LoadInGame
  GraphicsWindow.Clear()
  
  ' all doors are open in first room
  For i = 1 To 4
    room[i] = 1
  EndFor

  player["health"] = 110
  player["damage"] = 10
  player["armor"] = 10
  player["rooms"] = game["rooms"]
  player["new_damage"] = 0
  player["new_armor"] = 0
  player["health_poison"] = 0
  
  room_image = Shapes.AddImage(Program.Directory + "\room.png")
  doors[1] = Shapes.AddImage(Program.Directory + "\door_up.png")
  doors[2] = Shapes.AddImage(Program.Directory + "\door_left.png")
  doors[3] = Shapes.AddImage(Program.Directory + "\door_down.png")
  doors[4] = Shapes.AddImage(Program.Directory + "\door_right.png")
  chest_image = Shapes.AddImage(Program.Directory + "\chest.png")
  monster_image = Shapes.AddImage(Program.Directory + "\monster.png")
  
  hero_image = Shapes.AddImage(Program.Directory + "\hero.png")
  hero_equip_image = Shapes.AddImage(Program.Directory + "\hero_equipped.png")
  Shapes.Move(hero_image, 0, 610)
  Shapes.Move(hero_equip_image, 0, 610)
  Shapes.HideShape(hero_equip_image)
  
  room["open_door"] = 3
  EnterRoom()
  DrawGame()
EndSub

Sub UpdateInGame
  key = GraphicsWindow.LastKey
  If key = "Up" Then
    If room[1] = 1 Then
      room["open_door"] = 3
      EnterRoom()
    EndIf
  EndIf
  If key = "Right" Then
    If room[4] = 1 Then
      room["open_door"] = 2
      EnterRoom()
    EndIf
  EndIf
  If key = "Down" Then
    If room[3] = 1 Then
      room["open_door"] = 1
      EnterRoom()
    EndIf
  EndIf
  If key = "Left" Then
    If room[2] = 1 Then
      room["open_door"] = 4
      EnterRoom()
    EndIf
  EndIf
  
  DrawGame()

  If player["health"] < 1 Or player["rooms"] = 0 Then
    game["state"] = "postgame"
    LoadPostGame()
  ElseIf room["chest"] = 1 Then
    game["state"] = "open_chest"
    LoadChest()
  ElseIf room["monster"] = 1 Then
    game["state"] = "fight_monster"
    LoadFight()
  EndIf
EndSub

Sub EnterRoom
  room["chest"] = 0
  room["monster"] = 0
  
  For i = 1 To 4
    If i = room["open_door"] Then
      room[i] = 1
    Else
      If Math.GetRandomNumber(100) < 50 Then
        room[i] = 1
      Else
        room[i] = 0
      EndIf
    Endif  
  EndFor
 
  If player["rooms"] < game["rooms"] Then
    dice = Math.GetRandomNumber(12)
    If dice = 1 Or dice = 4 Or dice = 6 Then
      room["chest"] = 1
    ElseIf dice = 2 Or dice = 8 Then
      room["monster"] = 1
    EndIf
  EndIf
  
  player["health"] = player["health"] - 10
  player["rooms"] = player["rooms"] - 1
EndSub

Sub DrawGame
  DrawRoom()
  DrawUI()
EndSub

Sub DrawRoom
  Shapes.ShowShape(room_image)

  For i = 1 To 4
    If room[i] = 1 Then
      Shapes.ShowShape(doors[i])
    Else
      Shapes.HideShape(doors[i])
    EndIf
  EndFor
  
  If room["chest"] = 1 Then
    Shapes.ShowShape(chest_image)
  Else
    Shapes.HideShape(chest_image)
  EndIf
  
  If room["monster"] = 1 Then
    Shapes.ShowShape(monster_image)
  Else
    Shapes.HideShape(monster_image)
  EndIf
EndSub

Sub DrawUI
  GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(0, 0, 0)
  GraphicsWindow.FillRectangle(0, 0, GraphicsWindow.Width, GraphicsWindow.Height)
  GraphicsWindow.BrushColor = "Green"
  GraphicsWindow.FontSize = 16
  GraphicsWindow.FontBold = 0
  GraphicsWindow.DrawText(110, 610, "Здоровье: " + player["health"])
  GraphicsWindow.DrawText(110, 630, "Оружие: " + player["damage"])
  GraphicsWindow.DrawText(110, 650, "Броня: " + player["armor"])
  GraphicsWindow.DrawText(110, 670, "Этаж: " + (game["rooms"] - player["rooms"]))
  
  GraphicsWindow.BrushColor = "Yellow"
  GraphicsWindow.DrawText(400, 610, "Используйте стрелки, чтобы выбрать лестницу")
EndSub

'==========================================================
' OPEN CHEST STATE
'==========================================================
Sub LoadChest
    GraphicsWindow.BrushColor = "Red"
    GraphicsWindow.DrawText(400, 630, "Нажмите Q для открытия или E чтобы пропусить")
EndSub

Sub UpdateChest
  key = GraphicsWindow.LastKey
  If key = "E" Then
    IgnoreChest()
    
    game["state"] = "ingame"
    DrawGame()
  EndIf
  If key = "Q" Then
    OpenChest()
 
    game["state"] = "ingame"
    DrawGame()
    DrawChestResult()
  EndIf
EndSub

Sub OpenChest
  dice = Math.GetRandomNumber(12)
  room["chest"] = 0
  
  If dice = 1 Or dice = 3 Or dice = 5 Then
    armor = Math.GetRandomNumber(100)
    If armor > player["armor"] Then
      player["new_armor"] = armor
      player["armor"] = armor      
    EndIf
  ElseIf dice = 6 Or dice = 8 Or dice = 10 Then
    damage = Math.GetRandomNumber(100)
    If damage > player["damage"] Then
      player["new_damage"] = damage
      player["damage"] = damage      
    EndIf
  ElseIf dice = 4 Or dice = 11 Or dice = 12 Then
    player["health_poison"] = Math.GetRandomNumber(20)
    player["health"] = Math.Max(1, player["health"] - player["health_poison"])
  EndIf
EndSub

Sub IgnoreChest
  room["chest"] = 0
EndSub

Sub DrawChestResult
  If player["new_damage"] > 0 Then
    GraphicsWindow.BrushColor = "Green"
    GraphicsWindow.DrawText(400, 630, "Найдено новое оружие, урон увеличен " + player["new_damage"])
    player["new_damage"] = 0
  ElseIf player["new_armor"] > 0 Then
    GraphicsWindow.BrushColor = "Green"
    GraphicsWindow.DrawText(400, 630, "Найдена новая броня, защита увеличена " + player["new_armor"])
    player["new_armor"] = 0
    Shapes.ShowShape(hero_equip_image)
  ElseIf player["health_poison"] > 0 Then
    GraphicsWindow.BrushColor = "Red"
    GraphicsWindow.DrawText(400, 630, "Сундук отравлен, урон здоровью " + player["health_poison"])
    player["health_poison"] = 0
  Else
    GraphicsWindow.BrushColor = "Blue"
    GraphicsWindow.DrawText(400, 630, "Ничего не найдено")
  EndIf
EndSub

'==========================================================
' FIGHT MONSTER STATE
'==========================================================
Sub LoadFight
  GraphicsWindow.BrushColor = "Red"
  GraphicsWindow.DrawText(400, 630, "Нажмите Q для сражения или E чтобы сбежать")
EndSub

Sub UpdateFight
  key = GraphicsWindow.LastKey
  If key = "E" Then
    EscapeMonster()
    
    game["state"] = "ingame"    
    DrawGame()
    DrawEscapeResult()
  EndIf
  If key = "Q" Then
    FightMonster()

    game["state"] = "ingame"
    DrawGame()
    DrawFightResult()
  EndIf
EndSub

Sub FightMonster
  monster["stat_damage"] = Math.GetRandomNumber(100)
  monster["stat_health"] = Math.GetRandomNumber(100)
  monster["health"] = monster["stat_health"]
  fight["player_damage"] = 0
  fight["monster_damage"] = 0
  fight["player_health"] = 0
  room["monster"] = 0
  
  dice = Math.GetRandomNumber(12)
  If dice = 2 Or dice = 4 Or dice = 12 Then
    fight["turn"] = "игрок"
    While monster["health"] > 0 And player["health"] > 1
      player_dmg = Math.GetRandomNumber(player["damage"])
      player_arm = Math.GetRandomNumber(player["armor"])
      monster_dmg = Math.GetRandomNumber(monster["stat_damage"])
      fight["player_damage"] = fight["player_damage"] + player_dmg
      monster["health"] = Math.Max(0, monster["health"] - player_dmg)
      If monster["health"] > 0 Then
        fight["monster_damage"] = fight["monster_damage"] + monster_dmg
        player["health"] = Math.Max(1, player["health"] - monster_dmg + player_arm)
      EndIf
    EndWhile
  Else
    fight["turn"] = "монстр"
    While monster["health"] > 0 And player["health"] > 1
      player_dmg = Math.GetRandomNumber(player["damage"])
      player_arm = Math.GetRandomNumber(player["armor"])
      monster_dmg = Math.GetRandomNumber(monster["stat_damage"])
      fight["monster_damage"] = fight["monster_damage"] + monster_dmg
      player["health"] = Math.Max(1, player["health"] - monster_dmg + player_arm)
      If player["health"] > 1 Then
        fight["player_damage"] = fight["player_damage"] + player_dmg
        monster["health"] = Math.Max(0, monster["health"] - player_dmg)
      EndIf
    EndWhile
  EndIf
  
  If monster["health"] = 0 Then
    fight["player_health"] = Math.GetRandomNumber(100)
    player["health"] = player["health"] + fight["player_health"]
  EndIf
EndSub

Sub DrawFightResult
  GraphicsWindow.BrushColor = "Yellow"
  GraphicsWindow.DrawText(400, 630, "Атака монстра: " + monster["stat_damage"])
  GraphicsWindow.DrawText(400, 650, "Здоровье монстра: " + monster["stat_health"])

  GraphicsWindow.BrushColor = "Blue"
  GraphicsWindow.DrawText(400, 670, "Первым атаковал: " + fight["turn"])
  GraphicsWindow.DrawText(400, 690, "Урон получен: " + fight["monster_damage"])
  GraphicsWindow.DrawText(400, 710, "Урон нанесен: " + fight["player_damage"])
  
  If player["health"] = 1 Then
    GraphicsWindow.BrushColor = "Red"
    GraphicsWindow.DrawText(400, 730, "Монстр сбежал")
  Else
    GraphicsWindow.BrushColor = "Green"
    GraphicsWindow.DrawText(400, 730, "Монстр повержен")
    GraphicsWindow.DrawText(400, 750, "Здоровье увеличено на " + fight["player_health"])
  EndIf
EndSub

Sub EscapeMonster
  dice = Math.GetRandomNumber(12)
  escape["success"] = "False"
  room["monster"] = 0
  
  If dice = 1 Or dice = 4 Or dice = 6 Then
    escape["success"] = "True"
  Else
    escape["player_health"] = Math.GetRandomNumber(50)
    player["health"] = Math.Max(1, player["health"] - escape["player_health"])
  EndIf
EndSub

Sub DrawEscapeResult
  If escape["success"] = "True" Then
    GraphicsWindow.BrushColor = "Green"
    GraphicsWindow.DrawText(450, 630, "Вы сбежали")
  Else
    GraphicsWindow.BrushColor = "Red"
    GraphicsWindow.DrawText(450, 630, "Вам не удалось сбежать")
    GraphicsWindow.DrawText(450, 650, "Урон здоровью на " + escape["player_health"])
  EndIf
EndSub

'==========================================================
' GAME END STATE
'==========================================================
Sub LoadPostGame
  GraphicsWindow.Clear()
  
  GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(0, 50, 50)
  GraphicsWindow.FillRectangle(0, 0, GraphicsWindow.Width, GraphicsWindow.Height)
  GraphicsWindow.FontSize = 16
  GraphicsWindow.FontBold = 0
  
  If player["rooms"] = 0 Then
    GraphicsWindow.BrushColor = "Green"
    GraphicsWindow.DrawText(265, 300, "Поздравляем, вы сбежали из башни!")
  Else
    GraphicsWindow.BrushColor = "Red"
    GraphicsWindow.DrawText(220, 300, "Вы очень устали и не можете дальше идти")
  EndIf
  
  GraphicsWindow.BrushColor = "Yellow"
  GraphicsWindow.DrawText(200, 350, "Нажмите Q попробывать снова или Esc для выхода")
EndSub  

Sub UpdatePostGame
  key = GraphicsWindow.LastKey
  If key = "Q" Then
    game["state"] = "ingame"
    LoadInGame()
  EndIf
EndSub