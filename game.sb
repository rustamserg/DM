'==========================================================
' GAME INIT
'==========================================================
GraphicsWindow.KeyDown = HandleKey
GraphicsWindow.Clear()
GraphicsWindow.Title = "D.M."
GraphicsWindow.Height = 800
GraphicsWindow.Width = 800
GraphicsWindow.CanResize = "False"
GraphicsWindow.Show()

game["state"] = "pregame"
game["rooms"] = 20 ' number of rooms to escape
LoadPreGame()

'==========================================================
' GAME LOOP
'==========================================================
While ("True")
EndWhile

Sub HandleKey
  key = GraphicsWindow.LastKey
  If key = "Escape" Then
    Program.End()
  EndIf
  
  If game["state"] = "pregame" Then
    UpdatePreGame()
  EndIf
  If game["state"] = "ingame" Then
    UpdateInGame()
  EndIf
  If game["state"] = "open_chest" Then
    UpdateChest()
  EndIf
  If game["state"] = "fight_monster" Then
    UpdateFight()
  EndIf
  If game["state"] = "postgame" Then
    UpdatePostGame()
  EndIf
EndSub

'==========================================================
' START GAME STATE
'==========================================================
Sub LoadPreGame
  GraphicsWindow.Clear()
  
  GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(50, 50, 50)
  GraphicsWindow.FillRectangle(0, 0, GraphicsWindow.Width, GraphicsWindow.Height)
  GraphicsWindow.BrushColor = "Green"
  GraphicsWindow.FontSize = 16
  GraphicsWindow.FontBold = 0
  GraphicsWindow.DrawText(270, 300, "Evil wizard thrown you into the magic maze")
  GraphicsWindow.DrawText(280, 330, "You have to survive " + game["rooms"] + " rooms to escape")
  
  GraphicsWindow.BrushColor = "Yellow"
  GraphicsWindow.DrawText(320, 370, "Press Q to enter the maze")
EndSub

Sub UpdatePreGame
  key = GraphicsWindow.LastKey
  If key = "Q" Then
    game["state"] = "ingame"
    LoadInGame()
  EndIf
EndSub

'==========================================================
' GAME STATE
'==========================================================
Sub LoadInGame
  GraphicsWindow.Clear()
  
  ' all doors are open in first room
  For i = 1 To 4
    room[i] = 1
  EndFor

  player["health"] = 110
  player["damage"] = 10
  player["armor"] = 10
  player["rooms"] = game["rooms"]
  player["new_damage"] = 0
  player["new_armor"] = 0
  player["health_poison"] = 0
  
  room_image = Shapes.AddImage("C:\DM\room.png")
  doors[1] = Shapes.AddImage("C:\DM\door_up.png")
  doors[2] = Shapes.AddImage("C:\DM\door_left.png")
  doors[3] = Shapes.AddImage("C:\DM\door_down.png")
  doors[4] = Shapes.AddImage("C:\DM\door_right.png")
  chest_image = Shapes.AddImage("C:\DM\chest.png")
  monster_image = Shapes.AddImage("C:\DM\monster.png")
  
  room["open_door"] = 3
  EnterRoom()
  DrawGame()
EndSub

Sub UpdateInGame
  key = GraphicsWindow.LastKey
  If key = "Up" Then
    If room[1] = 1 Then
      room["open_door"] = 3
      EnterRoom()
    EndIf
  EndIf
  If key = "Right" Then
    If room[4] = 1 Then
      room["open_door"] = 2
      EnterRoom()
    EndIf
  EndIf
  If key = "Down" Then
    If room[3] = 1 Then
      room["open_door"] = 1
      EnterRoom()
    EndIf
  EndIf
  If key = "Left" Then
    If room[2] = 1 Then
      room["open_door"] = 4
      EnterRoom()
    EndIf
  EndIf
  
  DrawGame()

  If player["health"] < 1 Or player["rooms"] = 0 Then
    game["state"] = "postgame"
    LoadPostGame()
  ElseIf room["chest"] = 1 Then
    game["state"] = "open_chest"
    LoadChest()
  ElseIf room["monster"] = 1 Then
    game["state"] = "fight_monster"
    LoadFight()
  EndIf
EndSub

Sub EnterRoom
  room["chest"] = 0
  room["monster"] = 0
  
  For i = 1 To 4
    If i = room["open_door"] Then
      room[i] = 1
    Else
      If Math.GetRandomNumber(100) < 50 Then
        room[i] = 1
      Else
        room[i] = 0
      EndIf
    Endif  
  EndFor
 
  If player["rooms"] < game["rooms"] Then
    dice = Math.GetRandomNumber(12)
    If dice = 1 Or dice = 4 Or dice = 6 Then
      room["chest"] = 1
    ElseIf dice = 2 Or dice = 8 Then
      room["monster"] = 1
    EndIf
  EndIf
  
  player["health"] = player["health"] - 10
  player["rooms"] = player["rooms"] - 1
EndSub

Sub DrawGame
  DrawRoom()
  DrawUI()
EndSub

Sub DrawRoom
  Shapes.ShowShape(room_image)

  For i = 1 To 4
    If room[i] = 1 Then
      Shapes.ShowShape(doors[i])
    Else
      Shapes.HideShape(doors[i])
    EndIf
  EndFor
  
  If room["chest"] = 1 Then
    Shapes.ShowShape(chest_image)
  Else
    Shapes.HideShape(chest_image)
  EndIf
  
  If room["monster"] = 1 Then
    Shapes.ShowShape(monster_image)
  Else
    Shapes.HideShape(monster_image)
  EndIf
EndSub

Sub DrawUI
  GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(0, 0, 0)
  GraphicsWindow.FillRectangle(0, 0, GraphicsWindow.Width, GraphicsWindow.Height)
  GraphicsWindow.BrushColor = "Green"
  GraphicsWindow.FontSize = 16
  GraphicsWindow.FontBold = 0
  GraphicsWindow.DrawText(10, 610, "Health: " + player["health"])
  GraphicsWindow.DrawText(10, 630, "Damage: " + player["damage"])
  GraphicsWindow.DrawText(10, 650, "Armor: " + player["armor"])
  GraphicsWindow.DrawText(10, 670, "Rooms: " + player["rooms"])
  
  GraphicsWindow.BrushColor = "Yellow"
  GraphicsWindow.DrawText(300, 610, "Use arrow keys to move")
EndSub

'==========================================================
' OPEN CHEST STATE
'==========================================================
Sub LoadChest
    GraphicsWindow.BrushColor = "Red"
    GraphicsWindow.DrawText(300, 630, "Press Q to open the chest or E to ignore")
EndSub

Sub UpdateChest
  key = GraphicsWindow.LastKey
  If key = "E" Then
    IgnoreChest()
    
    game["state"] = "ingame"
    DrawGame()
  EndIf
  If key = "Q" Then
    OpenChest()
 
    game["state"] = "ingame"
    DrawGame()
    DrawChestResult()
  EndIf
EndSub

Sub OpenChest
  dice = Math.GetRandomNumber(12)
  room["chest"] = 0
  
  If dice = 1 Or dice = 3 Or dice = 5 Then
    armor = Math.GetRandomNumber(100)
    If armor > player["armor"] Then
      player["new_armor"] = armor
      player["armor"] = armor      
    EndIf
  ElseIf dice = 6 Or dice = 8 Or dice = 10 Then
    damage = Math.GetRandomNumber(100)
    If damage > player["damage"] Then
      player["new_damage"] = damage
      player["damage"] = damage      
    EndIf
  ElseIf dice = 4 Or dice = 11 Or dice = 12 Then
    player["health_poison"] = Math.GetRandomNumber(20)
    player["health"] = Math.Max(1, player["health"] - player["health_poison"])
  EndIf
EndSub

Sub IgnoreChest
  room["chest"] = 0
EndSub

Sub DrawChestResult
  If player["new_damage"] > 0 Then
    GraphicsWindow.BrushColor = "Green"
    GraphicsWindow.DrawText(300, 630, "Found new weapon, damage increased " + player["new_damage"])
    player["new_damage"] = 0
  ElseIf player["new_armor"] > 0 Then
    GraphicsWindow.BrushColor = "Green"
    GraphicsWindow.DrawText(300, 630, "Found new armor, increased to " + player["new_armor"])
    player["new_armor"] = 0
  ElseIf player["health_poison"] > 0 Then
    GraphicsWindow.BrushColor = "Red"
    GraphicsWindow.DrawText(300, 630, "Chest was poisoned, health damaged " + player["health_poison"])
    player["health_poison"] = 0
  Else
    GraphicsWindow.BrushColor = "Blue"
    GraphicsWindow.DrawText(300, 630, "Nothing found")
  EndIf
EndSub

'==========================================================
' FIGHT MONSTER STATE
'==========================================================
Sub LoadFight
  GraphicsWindow.BrushColor = "Red"
  GraphicsWindow.DrawText(300, 630, "Press Q to fight monster or E to escape")
EndSub

Sub UpdateFight
  key = GraphicsWindow.LastKey
  If key = "E" Then
    EscapeMonster()
    
    game["state"] = "ingame"    
    DrawGame()
    DrawEscapeResult()
  EndIf
  If key = "Q" Then
    FightMonster()

    game["state"] = "ingame"
    DrawGame()
    DrawFightResult()
  EndIf
EndSub

Sub FightMonster
  monster["stat_damage"] = Math.GetRandomNumber(100)
  monster["stat_health"] = Math.GetRandomNumber(100)
  monster["health"] = monster["stat_health"]
  fight["player_damage"] = 0
  fight["monster_damage"] = 0
  fight["player_health"] = 0
  room["monster"] = 0
  
  dice = Math.GetRandomNumber(12)
  If dice = 2 Or dice = 4 Or dice = 12 Then
    fight["turn"] = "player"
    While monster["health"] > 0 And player["health"] > 1
      player_dmg = Math.GetRandomNumber(player["damage"])
      player_arm = Math.GetRandomNumber(player["armor"])
      monster_dmg = Math.GetRandomNumber(monster["stat_damage"])
      fight["player_damage"] = fight["player_damage"] + player_dmg
      monster["health"] = Math.Max(0, monster["health"] - player_dmg)
      If monster["health"] > 0 Then
        fight["monster_damage"] = fight["monster_damage"] + monster_dmg
        player["health"] = Math.Max(1, player["health"] - monster_dmg + player_arm)
      EndIf
    EndWhile
  Else
    fight["turn"] = "monster"
    While monster["health"] > 0 And player["health"] > 1
      player_dmg = Math.GetRandomNumber(player["damage"])
      player_arm = Math.GetRandomNumber(player["armor"])
      monster_dmg = Math.GetRandomNumber(monster["stat_damage"])
      fight["monster_damage"] = fight["monster_damage"] + monster_dmg
      player["health"] = Math.Max(1, player["health"] - monster_dmg + player_arm)
      If player["health"] > 1 Then
        fight["player_damage"] = fight["player_damage"] + player_dmg
        monster["health"] = Math.Max(0, monster["health"] - player_dmg)
      EndIf
    EndWhile
  EndIf
  
  If monster["health"] = 0 Then
    fight["player_health"] = Math.GetRandomNumber(100)
    player["health"] = player["health"] + fight["player_health"]
  EndIf
EndSub

Sub DrawFightResult
  GraphicsWindow.BrushColor = "Yellow"
  GraphicsWindow.DrawText(300, 630, "Monster attack: " + monster["stat_damage"])
  GraphicsWindow.DrawText(300, 650, "Monster health: " + monster["stat_health"])

  GraphicsWindow.BrushColor = "Blue"
  GraphicsWindow.DrawText(300, 670, "First attack: " + fight["turn"])
  GraphicsWindow.DrawText(300, 690, "Damage received: " + fight["monster_damage"])
  GraphicsWindow.DrawText(300, 710, "Damage given: " + fight["player_damage"])
  
  If player["health"] = 1 Then
    GraphicsWindow.BrushColor = "Red"
    GraphicsWindow.DrawText(300, 730, "Monster is run out")
  Else
    GraphicsWindow.BrushColor = "Green"
    GraphicsWindow.DrawText(300, 730, "Monster is slained")
    GraphicsWindow.DrawText(300, 750, "Get food from monster " + fight["player_health"])
  EndIf
EndSub

Sub EscapeMonster
  dice = Math.GetRandomNumber(12)
  escape["success"] = "False"
  room["monster"] = 0
  
  If dice = 1 Or dice = 4 Or dice = 6 Then
    escape["success"] = "True"
  Else
    escape["player_health"] = Math.GetRandomNumber(50)
    player["health"] = Math.Max(1, player["health"] - escape["player_health"])
  EndIf
EndSub

Sub DrawEscapeResult
  If escape["success"] = "True" Then
    GraphicsWindow.BrushColor = "Green"
    GraphicsWindow.DrawText(300, 630, "You escaped")
  Else
    GraphicsWindow.BrushColor = "Red"
    GraphicsWindow.DrawText(300, 630, "You failed")
    GraphicsWindow.DrawText(300, 650, "Health damaged " + escape["player_health"])
  EndIf
EndSub

'==========================================================
' GAME END STATE
'==========================================================
Sub LoadPostGame
  GraphicsWindow.Clear()
  
  GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(0, 50, 50)
  GraphicsWindow.FillRectangle(0, 0, GraphicsWindow.Width, GraphicsWindow.Height)
  GraphicsWindow.FontSize = 16
  GraphicsWindow.FontBold = 0
  
  If player["rooms"] = 0 Then
    GraphicsWindow.BrushColor = "Green"
    GraphicsWindow.DrawText(265, 300, "Congrat, you escaped the maze")
  Else
    GraphicsWindow.BrushColor = "Red"
    GraphicsWindow.DrawText(220, 300, "You are too tired and failed to escape the maze")
  EndIf
  
  GraphicsWindow.BrushColor = "Yellow"
  GraphicsWindow.DrawText(260, 350, "Press Q to try again or Esc to exit")
EndSub  

Sub UpdatePostGame
  key = GraphicsWindow.LastKey
  If key = "Q" Then
    game["state"] = "ingame"
    LoadInGame()
  EndIf
EndSub